pragma ComponentBehavior: Bound

import Quickshell
import Quickshell.Io

import QtQuick
import QtQuick.Layouts

import qs.settings
import qs.helpers
import qs.widgets.common

Scope {
    id: root
    property bool osdVisible: false
    property string icon
    property string value

    function showOSD(type: int, icon: int, value: string): void {
        root.icon = Icons.get(icon);
        root.value = value;

        root.osdVisible = true;
        hideTimer.restart();
    }

    IpcHandler {
        target: "osd"
        function showOSD(type: int, iconName: string, value: string): void {
            root.showOSD(type, Icons.getKeyFromName, value);
        }
    }

    Timer {
        id: hideTimer
        interval: Variables.osdTimer
        onTriggered: root.osdVisible = false
    }

    LazyLoader {
        active: root.osdVisible

        PanelWindow {
            id: window
            implicitWidth: Variables.osdWidth
            implicitHeight: Variables.osdHeight
            anchors.bottom: true
            margins.bottom: screen.height / 7
            exclusiveZone: 0
            color: "transparent"
            mask: Region {}

            Rectangle {
                anchors.fill: parent
                color: Colors.background
                radius: Variables.osdRadius

                RowLayout {
                    anchors {
                        fill: parent
                        leftMargin: 18
                        rightMargin: 18
                    }
                    spacing: 12

                    Text {
                        font.pixelSize: Variables.iconSize
                        Layout.preferredHeight: Variables.iconSize
                        Layout.preferredWidth: Variables.iconSize
                        horizontalAlignment: Text.AlignLeft
                        verticalAlignment: Text.AlignVCenter
                        text: root.icon
                    }
                    Rectangle {
                        id: bar
                        Layout.fillWidth: true
                        Layout.preferredHeight: window.implicitHeight / 4
                        color: Colors.backgroundc
                        radius: Variables.osdRadius / 2

                        Rectangle {
                            anchors.top: parent.top
                            anchors.bottom: parent.bottom
                            implicitWidth: parseInt(root.value) / 100 * parent.width
                            color: Colors.aqua
                            radius: Variables.osdRadius / 2
                        }
                    }
                    Text {
                        text: root.value
                        font.pixelSize: 16
                        Layout.preferredWidth: parent.width / 6
                        horizontalAlignment: Text.AlignHCenter
                    }
                }
            }
        }
    }
}
